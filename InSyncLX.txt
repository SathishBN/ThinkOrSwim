# InSyncLX
# Drew Griffith

#hint: Mean Reversion EXIT Strategy. The default inputs are based on stocks that are more volatile in nature. If you prefer to trade less volatile stocks, you should lower the extremeties input. For good, long term trending stocks (above EMA300), the requirements for entry are not as strict in setting overbought/sold signals. For stocks that signal against the long term trend the entry requirements are more strict. The requirements for shorting signals are much more strict than long signals. Also, there are additional filters in place to ensure a better entry signal. The strategy is based on closing prices of the day of signal, so buy as close to the EOD as possible. The target is sumally the high price of the day of entry. Ideal hold times are less than 3-4 days. On day 3-4, the position moves to breakeven. Optimized for use on daily charts. This script portion that determines entry should be the same as InSync. The code for InSync.LX/SX has been removed so that code in the InSync entry will run faster in watchlist scans.

declare upper;

input BubbleOn = yes;
input extremities = 1.5; #percentage
def ema_length = 300;
def bb_length = 20;
def rsi2_length = 2;
input rsi2_oversold = 5;
def rsi14_length = 12;
def mfi_length = 14;
def bmp_length = 14;
def mdvl = 0.4;

## Does stock meet indicator requirements? (no shorting)
def bbCalc = BollingerPercentB(length = bb_length);
def rsi2 = RSI(length = rsi2_length);
def rsi14 = RSI(length = rsi14_length);
def mf = MoneyFlowIndex(length = mfi_length);
def bomp = BalanceOfMarketPower(length = bmp_length);

# Does stock meet ANY long term trend filters?
def ema = close > MovAvgExponential(length = ema_length);

# Does stock meet "bar" components?
def mdv = ((high - close) / (.001 + high - low) >= mdvl); ## long candle; close below midbody
def ext = ((high / close - 1) * 100) >= extremities; ## high/low must be greater than n pct from close
def gap = close <= close[1] and high > low[1]; ## do NOT trade against a gap

def signal1 = (ema)
  and mdv and ext #and gap
  and bbCalc < 5
  and rsi2 < rsi2_oversold
  and rsi14 < 40
  and mf < 35
  and bomp < 0;
def signal2 = (!ema)
  and mdv and ext #and gap
  and bbCalc < 5
  and rsi2 < rsi2_oversold
  and rsi14 < 30
  and mf < 30
  and bomp < 0;

## Plots
plot LE = if signal1 then 2 else if signal2 then 1 else 0;
LE.Hide();
LE.AssignValueColor(if signal1 then Color.GREEN else if signal2 then Color.CYAN else Color.GRAY);
LE.SetPaintingStrategy(PaintingStrategy.BOOLEAN_ARROW_UP);
LE.SetLineWeight(5);
AddChartBubble(BubbleOn and signal1, low, "Insync > EMA300", Color.GREEN, no);
AddChartBubble(BubbleOn and signal2, low, "Insync < EMA300", Color.CYAN, no);

def bullish =  LE > 0;

# Show price targets (if high is greater than 9%, then use midbody value of candle)
def pricetarget = if bullish and Average(100 * (high[0] / close[0] - 1), 1) >= (extremities) and Average(100 * (high[0] / close[0] - 1), 1) <= 9 then high
    else if bullish and Average(100 * (high[0] / close[0] - 1), 1) >= (extremities) and Average(100 * (high[0] / close[0] - 1), 1) > 9 then ((close[0] * (Average((high[0] / close[0] - 1), 1)) / 2) + close[0])
    else if bullish and Average(100 * (high[0] / close[0] - 1), 1) < (extremities) then ((close[0] * (extremities / 100)) + close[0])
    else Double.NaN;

def pt = if bullish then pricetarget
    else if bullish[1] then pricetarget[1]
    else if bullish[2] then pricetarget[2]
    else if bullish[3] then pricetarget[3]
    else if bullish[4] then pricetarget[4]
    else if bullish[5] then close[5]
    else Double.NaN;

plot target = pt;

target.SetPaintingStrategy(PaintingStrategy.DASHES);
target.SetLineWeight(3);
target.AssignValueColor(if high < pt then Color.RED else Color.WHITE);
