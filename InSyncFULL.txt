# InSync
# Drew Griffith

#hint: Mean Reversion ENTRY Strategy. The default inputs are based on stocks that are more volatile in nature. If you prefer to trade less volatile stocks, you should lower the extremeties input. This strategy looks for long term trending stocks above EMA300, SMA200 or linearregressionslope; Also, there are additional filters in place to ensure a better entry signal. The strategy is based on closing prices of the day of signal, so buy as close to the EOD as possible. The target is the high price of the day of entry. Ideal hold times are less than 5 days. On day 5, the position moves to breakeven. Optimized for use on daily charts.

declare lower;
input smooth = 1;

# Study Definitions
def bbCalc = BollingerPercentB();
def macd1 = MACD(8, 17);
def macd2 = macd1 - (MACD(8, 10) - MACD(17, 20));
def rsi = RSI(length = 2);
def change = RateOfChange(10);
def dpo = DetrendedPriceOsc();
def eom = EaseOfMovement();
def mf = MoneyFlowIndex();
def stoch = StochasticFull("k period" = 14);
def bomp = BalanceOfMarketPower();
def cc = CCI();

# Indicator Scoring
def bb = if (bbCalc > 95) then 10 else if (bbCalc < 5) then -10 else 0;
def cci = if cc > 100 then 10 else if cc < -100 then -10 else 0;
def roc = if change > 1 and change > ExpAverage(change, 10) then 10 else if change < 1 and change < ExpAverage(change, 10) then -10 else 0;
def sto = if stoch > 80 then 10 else if stoch < 20 then -10 else 0;
def rsiw = if rsi > 95 then 10 else if rsi < 5 then -10 else 0;
def macd = if macd1 > 0 and macd2 > 0 then 10 else if macd1 < 0 and macd2 < 0 then -10 else 0;
def bop = if bomp > 0 and bomp > ExpAverage(bomp, 10) then 10 else if bomp < 0 and bomp < ExpAverage(bomp, 10) then
-10 else 0;
def dp = if dpo > 0 then 10 else if dpo < 0 then -10 else 0;
def emv = if eom > 0 then 10 else if eom < 0 then -10 else 0;
def mfi = if mf > 80 then 10 else if mf < 35 then -10 else 0;

# Point Sum
def sum = bb + cci + roc + sto + rsiW + macd + bop + dp + emv + mfi;

# Plots
plot inSync = sum;
plot zero = 0;
plot pos55 = 90;
plot neg55 = -90;
inSync.AssignValueColor(if sum > 80 then Color.RED else if sum < -80 then Color.GREEN else Color.GRAY);
inSync.SetLineWeight(3);
zero.AssignValueColor(Color.LIGHT_GRAY);
pos55.AssignValueColor(Color.GREEN);
neg55.AssignValueColor(Color.RED);
zero.HideTitle();
pos55.HideTitle();
neg55.HideTitle();

plot signal = if sum == 100 then 100 else if sum == -100 then -100 else Double.NaN;
signal.SetPaintingStrategy(paintingStrategy = PaintingStrategy.POINTS);
signal.SetLineWeight(4);
signal.AssignValueColor(Color.YELLOW);

# Alerts
#Alert((sum == -100), "inSync LE", "alert type" = Alert.BAR, sound = Sound.Ding);
#Alert((sum == 100), "inSync SE", "alert type" = Alert.BAR, sound = Sound.Ding);
