# RSI Divergence
# Drew Griffith

#hint: Signal if the RSI is in a divergence.

declare lower;

input nRSI = 2;
input OverBought = 95;
input OverSold = 5;
input MidLine = 50;
input nTrend = 100;
input TrendLine = {EMA, SMA, default LRL, WMA};

# Global definitions
def h = high;
def l = low;
def c = close;

def cond1 = CompoundValue(1, if IsNaN(c)
                            then cond1[1]
                            else c, c);

# RSI Wilder with Divergence Markers
def NetChgAvg = WildersAverage(c - c[1], nRSI);
def TotChgAvg = WildersAverage(AbsValue(c - c[1]), nRSI);
def ChgRatio = if TotChgAvg != 0 then NetChgAvg / TotChgAvg else 0;
plot RSI = Round(50 * (ChgRatio + 1), 2);
RSI.SetLineWeight(2);
RSI.AssignValueColor(if RSI < OverSold then Color.GREEN else if  RSI > OverBought then Color.RED else Color.GRAY);

plot RSItrend;
switch (TrendLine) {
case EMA:
    RSItrend = Round(ExpAverage(RSI, nTrend), 2);
case SMA:
    RSItrend = Round(Average(RSI, nTrend), 2);
case LRL:
    RSItrend = Round(InertiaAll(RSI, nTrend), 2);
case WMA:
    RSItrend = Round(WMA(RSI, nTrend), 2);
}
RSItrend.SetLineWeight(1);
RSItrend.SetDefaultColor(Color.RED);

plot RSIOB = OverBought;
RSIOB.SetLineWeight(1);
RSIOB.SetDefaultColor(Color.DARK_GRAY);
plot RSIOS = OverSold;
RSIOS.SetLineWeight(1);
RSIOS.SetDefaultColor(Color.DARK_GRAY);

def lowestLow = if RSI > OverSold
               then l
               else if RSI < OverSold and
                       l < lowestLow[1]
               then l
               else lowestLow[1];
def lowestRSI = if RSI > MidLine
               then RSI
               else if RSI < MidLine and
                       RSI < lowestRSI[1]
               then RSI
               else lowestRSI[1];
def divergentLow = if RSI < OverSold and
                  l <= lowestLow[1] and
                  RSI > lowestRSI[1]
                  then OverSold
                  else Double.NaN;
plot DLow = divergentLow;
DLow.SetPaintingStrategy(PaintingStrategy.POINTS);
DLow.SetLineWeight(3);
DLow.SetDefaultColor(Color.YELLOW);

def highestHigh = if RSI < OverBought
                 then h
                 else if RSI > OverBought and
                         h > highestHigh[1]
                 then h
                 else highestHigh[1];
def highestRSI = if RSI < MidLine
                then RSI
                else if RSI > MidLine and
                        RSI > highestRSI[1]
                then RSI
                else highestRSI[1];
def divergentHigh = if RSI > OverBought and
                      h >= highestHigh[1] and
                      RSI < highestRSI[1] and
                      cond1 within 3 bars
                   then OverBought
                   else Double.NaN;

plot DHigh = divergentHigh;
DHigh.SetPaintingStrategy(PaintingStrategy.POINTS);
DHigh.SetLineWeight(3);
DHigh.SetDefaultColor(Color.YELLOW);

#def AlertCond1 = DHigh == OverBought;
#def AlertCond2 = DLow == OverSold;

#Alert(AlertCond1, "Divergent High", Alert.Bar, Sound.Bell);
#Alert(AlertCond1, "Divergent Low", Alert.Bar, Sound.Bell);
