# Market Regime
# Drew Griffith

# hint: Determine which market regime. All calcs based on SPY closing price. Regime rules based on research by Cesar Alvarez.

declare upper;
input sym = "SPY";
input averageType = AverageType.EXPONENTIAL;
input ma_length = 300;
input hv_length = 100;

def cl = close(sym);

# Historical Volatility
def hv = 100 * StDev(log(cl/cl[1]),hv_length) * sqrt(252);

# Moving Average
def long_trend = MovingAverage(averageType, cl, ma_length);

#Quiet Bull
plot regime = if cl > long_trend and long_trend > long_trend[21] and hv < 15 then 1 #Quiet Bull - 46.2% of the market days
  else if cl < long_trend and long_trend < long_trend[21] and hv < 15 then 2 #Quiet Bear - 0.1% of the market days
  else if cl > long_trend and long_trend < long_trend[21] and hv < 15 then 3 #Quiet Trendless - 3.5% of the market days
  else if cl < long_trend and long_trend > long_trend[21] and hv < 15 then 3 #Quiet Trendless - 3.5% of the market days
  else if cl > long_trend and long_trend > long_trend[21] and hv > 15 then 4 #Volatile Bull - 14.7% of the market days
  else if cl < long_trend and long_trend < long_trend[21] and hv > 15 then 5 #Volatile Bear - 27.5% of the market days
  else if cl > long_trend and long_trend < long_trend[21] and hv > 15 then 6 #Volatile Trendless - 8.0% of the market days
  else if cl < long_trend and long_trend > long_trend[21] and hv > 15 then 6 #Volatile Trendless - 8.0% of the market days
  else double.nan;
regime.hide();

# Add label
AddLabel(regime, "Market Regime: " + if regime == 1 then "Quiet Bull"
  else if regime == 2 then "Quiet Bear"
  else if regime == 3 then "Quiet Trendless"
  else if regime == 4 then "Volatile Bull"
  else if regime == 5 then "Volatile Bear"
  else if regime == 6 then "Volatile Trendless"
  else "Unknown",
  if regime == 1 then Color.GREEN
  else if regime == 2 then Color.RED
  else if regime == 3 then Color.Gray
  else if regime == 4 then Color.GREEN
  else if regime == 5 then Color.RED
  else if regime == 6 then Color.Gray
  else Color.Gray);
