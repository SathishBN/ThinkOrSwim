# Market Regime
# Drew Griffith

# hint: Determine which market regime. All calcs based on SPY closing price. Regime rules based on research by Cesar Alvarez.

declare upper;
input sym = "SPY";
input averageType = AverageType.EXPONENTIAL;
input basis = {default Annual, Monthly, Weekly, Daily};
input ma_length = 300;
input length = 100;

def cl = close(sym);

def ap = getAggregationPeriod();

assert(ap >= AggregationPeriod.MIN, "Study can only be calculated for time-aggregated charts: " + ap);

def barsPerDay = (regularTradingEnd(getYyyyMmDd()) - regularTradingStart(getYyyyMmDd())) / ap;
def barsPerYear =
    if ap > AggregationPeriod.WEEK then 12
    else if ap == AggregationPeriod.WEEK then 52
    else if ap >= AggregationPeriod.DAY then 252 * AggregationPeriod.DAY / ap
    else 252 * barsPerDay;

def basisCoeff;
switch (basis) {
case Annual:
    basisCoeff = 1;
case Monthly:
    basisCoeff = 12;
case Weekly:
    basisCoeff = 52;
case Daily:
    basisCoeff = 252;
}

def clLog = log(cl / cl[1]);
def HV = 100 * stdev(clLog, length) * Sqrt(barsPerYear / basisCoeff * length / (length - 1));

# Moving Average
def long_trend = MovingAverage(averageType, cl, ma_length);

#Quiet Bull
plot regime = if cl > long_trend and long_trend > long_trend[21] and hv < 15 then 1 #Quiet Bull - 46.2% of the market days
  else if cl < long_trend and long_trend < long_trend[21] and hv < 15 then 2 #Quiet Bear - 0.1% of the market days
  else if cl > long_trend and long_trend < long_trend[21] and hv < 15 then 3 #Quiet Trendless - 3.5% of the market days
  else if cl < long_trend and long_trend > long_trend[21] and hv < 15 then 3 #Quiet Trendless - 3.5% of the market days
  else if cl > long_trend and long_trend > long_trend[21] and hv > 15 then 4 #Volatile Bull - 14.7% of the market days
  else if cl < long_trend and long_trend < long_trend[21] and hv > 15 then 5 #Volatile Bear - 27.5% of the market days
  else if cl > long_trend and long_trend < long_trend[21] and hv > 15 then 6 #Volatile Trendless - 8.0% of the market days
  else if cl < long_trend and long_trend > long_trend[21] and hv > 15 then 6 #Volatile Trendless - 8.0% of the market days
  else double.nan;
regime.hide();

# Add label
AddLabel(regime, "Market Regime: " + if regime == 1 then "Quiet Bull"
  else if regime == 2 then "Quiet Bear"
  else if regime == 3 then "Quiet Trendless"
  else if regime == 4 then "Volatile Bull"
  else if regime == 5 then "Volatile Bear"
  else if regime == 6 then "Volatile Trendless"
  else "Unknown",
  if regime == 1 then Color.GREEN
  else if regime == 2 then Color.RED
  else if regime == 3 then Color.Gray
  else if regime == 4 then Color.GREEN
  else if regime == 5 then Color.RED
  else if regime == 6 then Color.Gray
  else Color.Gray);
