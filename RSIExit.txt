# RSI Exit
# Drew Griffith

#hint: RSI2 is a common way to determine exit if you incorporate it into your buy signal for a mean reversion strategy. Even on a loss, the RSI indicates a lower high sell point. I see this as selling at a high for a stop loss.

# 70 for long / 30 for short for Daily chart
# 80 for long / 20 for short for 30min chart
# 90 for long / 10 for short for 15min chart

input side = {DEFAULT LONG, SHORT};
input target = 70;
input rsi_length = 2;
input price = close;

# rsi
def netchgavg = wildersaverage(price - price[1], rsi_length);
def totchgavg = wildersaverage(absvalue(price - price[1]), rsi_length);
def chgratio = if totchgavg != 0 then netchgavg / totchgavg else 0;
def rsi = round(50 * (chgratio + 1), numberofdigits = 0);

def lmt;
switch (side) {
case long:
    lmt = rsi >= target;
case short:
    lmt = rsi <= target;
}

plot exit = lmt;
exit.setdefaultcolor(createcolor(255, 255, 255));
exit.setpaintingstrategy(paintingstrategy.boolean_arrow_down);
