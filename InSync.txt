# InSync
# Drew Griffith

#hint: Mean Reversion ENTRY Strategy. The default inputs are based on stocks that are more volatile in nature. If you prefer to trade less volatile stocks, you should lower the extremities input. This strategy looks for long term trending stocks above EMA300, SMA200; Also, there are additional filters in place to ensure a better entry signal. The strategy is based on closing prices of the day of signal, so buy as close to the EOD as possible. The target is the high price of the day of entry. Ideal hold times are less than 5 days. On day 5, the position moves to breakeven. Optimized for use on daily charts.

declare upper;

input volChecker = NO;
input audibleAlerts = NO;
input extremities = 2.0; #0.5-1.0 intraday
input innerValue = 1.5;
input outerValue = 2.0;
input atr_length = 12;
input bb_length = 20;
input mfi_length = 14;
def rsi2_length = 2;
def rsi2_oversold = 5;
def price = close;
def averageType = AverageType.EXPONENTIAL;
def trueRangeAverageType = AverageType.EXPONENTIAL;

# ATR Band Calc (OuterBands removed on scan)
def OuterValueshift = outerValue * MovingAverage(trueRangeAverageType, TrueRange(high, close, low), atr_length);
def InnerValueshift = innerValue * MovingAverage(trueRangeAverageType, TrueRange(high, close, low), atr_length);
def average = MovingAverage(averageType, price, atr_length);
def LowerOuterBand = average - OuterValueshift;
def LowerInnerBand = average - InnerValueshift;

# Large move down
def ext = ((high / close - 1) * 100) >= extremities; ## high must be greater than n pct from close
def bbCalc = BollingerPercentB(length = bb_length);
def rsi2 = RSI(length = rsi2_length);
def mf = MoneyFlowIndex(length = mfi_length);
def vol_req = if volChecker and VolumeAvg() < VolumeAvg()[1] then 1
    else if !volChecker then 1 else 0;

plot LE = ext
  and vol_req
  and price < LowerInnerBand
  and bbCalc < 5
  and rsi2 < rsi2_oversold
  and mf < 35;
LE.AssignValueColor(if LE and price < LowerOuterBand then Color.GREEN else if LE and price < LowerInnerBand then Color.CYAN else Color.GRAY);
LE.SetPaintingStrategy(PaintingStrategy.BOOLEAN_ARROW_UP);
LE.SetLineWeight(5);

Alert(audibleAlerts and LE, GetSymbol() + " at exhaustion.", Alert.BAR, Sound.Ding);

## Needed for Watchlist box painting
#AssignBackgroundColor(if LE then Color.GREEN else Color.GRAY);
